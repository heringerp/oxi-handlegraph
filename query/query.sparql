PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX vg: <http://biohackathon.org/resource/vg#>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX fhir: <http://hl7.org/fhir/>

SELECT
  ?node
(AVG(?osteo_count) AS ?avg_osteo)
WHERE {
  {
SELECT
   ?path
  (SUM(?osteo) AS ?osteo_count)
WHERE {
   SERVICE <http://localhost:3030/Patient_FHIR> {
     {
       SELECT
         (SUBSTR(?pat, 9) AS ?pat_id)
         ?osteo
       WHERE {
         ?con fhir:Condition.code [ fhir:CodeableConcept.text [ fhir:value ?cond ] ] .
         ?con fhir:Condition.subject [ fhir:Reference.reference [ fhir:value ?pat ] ] .
         BIND(xsd:integer(?cond = "Osteroporose") AS ?osteo)
       }
     }
     ?patient fhir:Patient.identifier [ fhir:Identifier.value [ fhir:value ?pat_id ] ] .
     ?patient fhir:sampleName ?sample .
   }
   ?path rdf:type vg:Path .
   FILTER( CONTAINS( STR(?path), ?sample ) ) .
}
GROUP BY ?path
  }
  ?step vg:path ?path .
  ?step vg:node ?node .
}
GROUP BY ?node
HAVING (AVG(?osteo_count) = 1)
